#  Copyright (C) 2007 National Institute For Space Research (INPE) - Brazil.
#
#  This file is part of TerraMA2 - a free and open source computational
#  platform for analysis, monitoring, and alert of geo-environmental extremes.
#
#  TerraMA2 is free software: you can redistribute it and/or modify
#  it under the terms of the GNU Lesser General Public License as published by
#  the Free Software Foundation, either version 3 of the License,
#  or (at your option) any later version.
#
#  TerraMA2 is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#  GNU Lesser General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with TerraMA2. See LICENSE. If not, write to
#  TerraMA2 Team at <terrama2-team@dpi.inpe.br>.
#
#
#  Description: FixBundle utility.
#
#  Author: Gilberto Ribeiro de Queiroz
#          Vinicius Campanha
#

include(BundleUtilities)

set(TERRAMA2_BUILD_AS_BUNDLE @TERRAMA2_BUILD_AS_BUNDLE@)
set(TERRAMA2_DESTINATION_RUNTIME @TERRAMA2_DESTINATION_RUNTIME@)
set(TERRAMA2_DESTINATION_LIBRARY @TERRAMA2_DESTINATION_LIBRARY@)
set(Qt5_DIR @Qt5_DIR@)


# List TerraMa2 Libraries
file(GLOB_RECURSE TERRAMA2_LIBS ${CMAKE_INSTALL_PREFIX}/libterrama2*@CMAKE_SHARED_LIBRARY_SUFFIX@)

# List TerraLib Libraries
file(GLOB_RECURSE TERRALIB_PLGS ${CMAKE_INSTALL_PREFIX}/libterralib_mod*@CMAKE_SHARED_LIBRARY_SUFFIX@)


if(WIN32)
  set(TERRAMA2_LIBS_LOCATION ${CMAKE_INSTALL_PREFIX}/${TERRAMA2_DESTINATION_RUNTIME})
else()
  set(TERRAMA2_LIBS_LOCATION ${CMAKE_INSTALL_PREFIX}/${TERRAMA2_DESTINATION_LIBRARY})
endif()

set(TERRAMA2_APPLICATION "")
set(QT_PLUGINS "")

if(WIN32)

  set(TERRAMA2_APPLICATION "${CMAKE_INSTALL_PREFIX}/bin/terrama2_admin@CMAKE_EXECUTABLE_SUFFIX@")
  set(QT_PLUGINS "${CMAKE_INSTALL_PREFIX}/qtplugins")
  set(TERRAMA2_DIRS "@Qt5_DIR@/../..")

elseif(APPLE AND TERRAMA2_BUILD_AS_BUNDLE)
    
    set(BU_CHMOD_BUNDLE_ITEMS ON)
    set(TERRAMA2_APPLICATION "${CMAKE_INSTALL_PREFIX}/terrama2_service.app")
    set(QT_PLUGINS "${TERRAMA2_APPLICATION}/Contents/qtplugins")
    set(TERRAMA2_DIRS "@terralib_DIR@/../..;@Qt5_DIR@/../..")
    
endif()

if(QT_PLUGINS)
  file(GLOB_RECURSE QT_PLUGINS "${QT_PLUGINS}/*@CMAKE_SHARED_LIBRARY_SUFFIX@")
endif()

set(TERRAMA2_LIBRARIES ${TERRALIB_PLGS} ${QT_PLUGINS} "${TERRAMA2_APPLICATION}/Contents/MacOS/terrama2_service")

if(APPLE AND TERRAMA2_BUILD_AS_BUNDLE)

  # Overload function
  function(gp_item_default_embedded_path item default_embedded_path_var)
    set (is_System FALSE)
    set (is_QImageFormat FALSE)
    set (is_QIconEngine FALSE)
    set (is_QSQLDrivers FALSE)
    set (is_QPlatforms FALSE)
    set (is_Lib FALSE)
    
     get_filename_component (resolved_file ${item} PATH)
     
     if("${resolved_file}" STREQUAL "")
       set (is_Lib TRUE)
     elseif(resolved_file MATCHES "^(@rpath)")
       set(is_Lib TRUE)
     elseif(resolved_file MATCHES "^(/System/Library/|/usr/lib/|/usr/local|@loader_path)")
       set(is_System TRUE)
     elseif(resolved_file MATCHES "(imageformats)$")
       set (is_QImageFormat TRUE)
     elseif(resolved_file MATCHES "(iconengines)$")
       set (is_QIconEngine TRUE)
     elseif(resolved_file MATCHES "(sqldrivers)$")
       set (is_QSQLDrivers TRUE)
     elseif(resolved_file MATCHES "(platforms)$")
       set (is_QPlatforms TRUE)
     elseif(resolved_file MATCHES "(lib)$")
       set (is_Lib TRUE)
     endif()
    
    if(item MATCHES "\\.dylib$")
      if(is_System)
        set(${default_embedded_path_var} "@executable_path/../lib" PARENT_SCOPE)
      elseif(is_QImageFormat)
        set(${default_embedded_path_var} "@executable_path/../qtplugins/imageformats" PARENT_SCOPE)
      elseif(is_QIconEngine)
        set(${default_embedded_path_var} "@executable_path/../qtplugins/iconengines" PARENT_SCOPE)
      elseif(is_QSQLDrivers)
        set(${default_embedded_path_var} "@executable_path/../qtplugins/sqldrivers" PARENT_SCOPE)
      elseif(is_QPlatforms)
        set(${default_embedded_path_var} "@executable_path/../qtplugins/platforms" PARENT_SCOPE)
      elseif(is_Lib)
        set(${default_embedded_path_var} "@executable_path/../lib" PARENT_SCOPE)
      else()
        set(${default_embedded_path_var} "${resolved_file}" PARENT_SCOPE)
      endif()
    elseif(item MATCHES "[^/]+\\.framework/")
      set(${default_embedded_path_var} "@executable_path/../Frameworks" PARENT_SCOPE)
    endif()
  endfunction()

endif()

# Call fixup_bundle
fixup_bundle("${TERRAMA2_APPLICATION}" "${TERRAMA2_LIBRARIES}" "${TERRAMA2_DIRS}")

